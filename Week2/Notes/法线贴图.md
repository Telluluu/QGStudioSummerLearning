# 法线贴图

法线贴图的原理在前一周已经讲述过，这里不再赘述

### 要使用法线贴图：

1. 首先需要一张法线贴图，并在C++端创建一个`ID3D11Texture2D`
2. 对应用法线贴图的模型，顶点数据需要包含位置、法向量、切线向量、纹理坐标
3. 在顶点着色器中将顶点法向量和切向量从局部坐标系变换到世界坐标系
4. 在像素着色器中，使用经过线性插值的法向量和切向量为每个三角形表面的像素点构建TBN坐标系，然后将切线空间的法向量变换到世界坐标系，用于光照计算

### HLSL代码的变化:

#### 新增了一个法线贴图采样器，和两个包含切向量的结构体

```HLSL
Texture2D g_NormalMap : register(t1);

struct VertexPosNormalTangentTex
{
    float3 posL : POSITION;
    float3 normalL : NORMAL;
    float4 tangentL : TANGENT;
    float2 tex : TEXCOORD;
};

struct VertexPosHWNormalTangentTex
{
    float4 posH : SV_POSITION;
    float3 posW : POSITION; // 在世界中的位置
    float3 normalW : NORMAL; // 法向量在世界中的方向
    float4 tangentW : TANGENT; // 切线在世界中的方向
    float2 tex : TEXCOORD;
};
```



#### 在法线纹理的顶点着色器中

```HLSL
    vOut.tangentW = float4(mul(vIn.tangentL.xyz, (float3x3) g_World), vIn.tangentL.w);
```

**新增一行,将切向量转换到世界坐标系**



#### 在法线纹理的像素着色器中

```HLSL
    // 法线映射
    float3 normalMapSample = g_NormalMap.Sample(g_Sam, pIn.tex).rgb;
    float3 bumpedNormalW = NormalSampleToWorldSpace(normalMapSample, pIn.normalW, pIn.tangentW);
```

```HLSL
float3 NormalSampleToWorldSpace(float3 normalMapSample,
    float3 unitNormalW,
    float4 tangentW)
{
    // 将读取到法向量中的每个分量从[0, 1]还原到[-1, 1]
    float3 normalT = 2.0f * normalMapSample - 1.0f;

    // 构建位于世界坐标系的切线空间
    float3 N = unitNormalW;
    float3 T = normalize(tangentW.xyz - dot(tangentW.xyz, N) * N); // 施密特正交化
    float3 B = cross(N, T);

    float3x3 TBN = float3x3(T, B, N);

    // 将凹凸法向量从切线空间变换到世界坐标系
    float3 bumpedNormalW = mul(normalT, TBN);

    return bumpedNormalW;
}
```

这里用到的`NormalSampleToWorldSpace`函数接收三个参数，**采样法线纹理得到的向量，法向量和切向量，将法向量从切线空间转换到世界坐标系**

得到的`bumpedNormalW`就可以用于光照计算了