# 几何着色器

几何着色器是在光栅化阶段之前，最后一个可以操作顶点的阶段，它可以做如下事情：

1. 让程序自动决定如何在渲染管线中插入/移除几何体；
2. 通过流输出阶段将顶点信息再次传递到顶点缓冲区；
3. 改变图元类型（如输入点图元，输出三角形图元）；

但它也有缺点，几何着色器输出的顶点数据很可能是有较多重复的，从流输出拿回到顶点缓冲区的话会占用较多的内存空间。它本身无法输出索引数组。

离开几何着色器的顶点如果要传递给光栅化阶段，需要包含语义SV_POSITION

##### 一个几何着色器代码：

```HLSL
struct GSOutput
{
    float4 pos : SV_POSITION;
};

[maxvertexcount(3)]
void main(
    triangle float4 input[3] : SV_POSITION, 
    inout TriangleStream< GSOutput > output
)
{
    for (uint i = 0; i < 3; i++)
    {
        GSOutput element;
        element.pos = input[i];
        output.Append(element);
    }
}
```

几何着色器是根据图元类型来进行调用，若使用的是TriangleList，则每一个三角形的三个顶点都会作为输入，触发几何着色器的调用。

我们必须要指定它每次调用所允许输出的最大顶点数目，可以使用属性语法来强行修改着色器行为

`[maxvertexcount(N)]`

N并表示每次调用允许产出的最大顶点数目，最终输出的顶点数不会超过N。`maxvertexcount`的值应当经可能的小,因为它将直接决定几何着色器的运行效率

几何着色器的每次调用最多只能处理1024个<font size=5>标量</font>,例如只包含4D位置向量的顶点，最多只能处理256个顶点

几何着色器输入的结构体类型不允许超过128个<font size=5>标量</font>，例如只包含4D位置向量的结构体，最多只能包含32个顶点

代码中的triangle用于指定输入的图元类型，具体支持的关键字如下

| 图元类型    | 描述                                                         |
| ----------- | ------------------------------------------------------------ |
| point       | Point list                                                   |
| line        | Line list or line strip                                      |
| triangle    | Triangle list or triangle strip                              |
| lineadj     | Line list with adjacency or line strip with adjacency        |
| triangleadj | Triangle list with adjacency or triangle strip with adjacency |

<font size=5>参数类型</font>可以是用户自定义的结构体，或者是向量(float4)类型。从顶点着色器传过来的顶点至少会包含一个表示齐次裁剪坐标的向量

<font size=5>参数名</font>`input`也是可以任意指定的，对于该输入参数的元素数目，取决于前面声明的图元类型

| 图元类型    | 元素数目                      |
| ----------- | ----------------------------- |
| point       | [1] 每次只能处理1个顶点       |
| line        | [2] 一个线段必须包含2个顶点   |
| triangle    | [3] 一个三角形需要3个顶点     |
| lineadj     | [4] 一个邻接线段需要4个顶点   |
| triangleadj | [6] 一个邻接三角形需要6个顶点 |

第二个参数必须是<font size=5>流输出对象</font>，而且需要指定为`inout`可读写类型它是一个类模板，模板的形参指定要输出的类型，流输出对象有如下三种

| 流输出对象类型 | 描述               |
| -------------- | ------------------ |
| PointStream    | 一系列点的图元     |
| LineStream     | 一系列线段的图元   |
| TriangleStream | 一系列三角形的图元 |

流输出对象都具有下面两种<font size=5>方法</font>：

| 方法         | 描述                                                         |
| ------------ | ------------------------------------------------------------ |
| Append       | 向指定的流输出对象添加一个输出的数据                         |
| RestartStrip | 在以线段或者三角形作为图元的时候，默认是以strip的形式输出的， 如果我们不希望下一个输出的顶点与之前的顶点构成新图元，则需要 调用此方法来重新开始新的strip。若希望输出的图元类型也保持和原 来一样的TriangleList，则需要每调用3次Append方法后就调用一次 RestartStrip。 |

```HLSL
// Triangle_GS.hlsl
#include "Basic.hlsli"

[maxvertexcount(9)]
void GS(triangle VertexPosHColor input[3], inout TriangleStream<VertexPosHColor> output)
{
    //
    // 将一个三角形分裂成三个三角形，即没有v3v4v5的三角形
    //       v1
    //       /\
    //      /  \
    //   v3/____\v4
    //    /\xxxx/\
    //   /  \xx/  \
    //  /____\/____\
    // v0    v5    v2


    VertexPosHColor vertexes[6];
    int i;
    [unroll]
    for (i = 0; i < 3; ++i)
    {
        vertexes[i] = input[i];
        vertexes[i + 3].Color = (input[i].Color + input[(i + 1) % 3].Color) / 2.0f;
        vertexes[i + 3].PosH = (input[i].PosH + input[(i + 1) % 3].PosH) / 2.0f;
    }

    [unroll]
    for (i = 0; i < 3; ++i)
    {
        output.Append(vertexes[i]);
        output.Append(vertexes[3 + i]);
        output.Append(vertexes[(i + 2) % 3 + 3]);
        output.RestartStrip();

    }
}
```

示例中的几何着色器代码，在循环中新增了v3，v4，v5三个顶点，pos，color信息线性插值，然后用append和RestartRStrip方法将9个顶点组合成三个新的三角形