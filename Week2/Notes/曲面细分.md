# 曲面细分

曲面细分是D3D11新增的功能，它能将几何体细分为更小的三角形，并把新生成的顶点偏移到合适的位置，以增加三角形数量丰富网格细节

曲面细分技术包含三个阶段，外壳着色器、曲面细分阶段、域着色器，曲面细分技术是可选的，但如果使用曲面细分技术，就必须经历这三个阶段

## 外壳着色器

外壳着色器由<font size=5>常量外壳着色器(Constant Hull Shader)</font>和<font size=5>控制点外壳着色器(Control Point Hull Shader)</font>组成

### 常量外壳着色器

常量外壳着色器会针对每个面片统一处理。它会输出当前网格的**曲面细分因子**，而且必须输出。**曲面细分因子**指示了在曲面细分阶段中将面片镶嵌处理后的分数，以及怎么进行输出。它由两个输出系统值所表示：`SV_TessFactor`和`SV_InsideTessFactor`，这两个输出系统值属于float或float数组，具体取决于输入装配阶段定义的图元类型。常量外壳着色器的输出被限制在128个标量，除了系统值，还可以额外添加输出信息供每个面片所使用

```HLSL
struct QuadPatchTess
{
    float EdgeTess[4] : SV_TessFactor;
    float InsideTess[2] : SV_InsideTessFactor;
    
    // 可以在下面为每个面片附加所需的额外信息
};
```

**↑常量外壳着色器的输出结构体↑**

```HLSL
QuadPatchTess QuadConstantHS(InputPatch<VertexOut, 4> patch, uint patchID : SV_PrimitiveID)
{
    QuadPatchTess pt;
    
    pt.EdgeTess[0] = g_QuadEdgeTess[0];            // 四边形面片的左侧边缘
    pt.EdgeTess[1] = g_QuadEdgeTess[1];            // 四边形面片的上侧边缘
    pt.EdgeTess[2] = g_QuadEdgeTess[2];            // 四边形面片的右侧边缘
    pt.EdgeTess[3] = g_QuadEdgeTess[3];            // 四边形面片的下册边缘
    pt.InsideTess[0] = g_QuadInsideTess[0];        // u轴(四边形内部细分的列数)
    pt.InsideTess[1] = g_QuadInsideTess[1];        // v轴(四边形内部细分的行数)
    
    return pt;
}
```

**<font size=4>通过常量缓冲区来为其设置各个方面的细分程度</font>**

1. `InputPatch<VertexOut, 4>`定义了控制点的数目和信息，控制点首先会传至到顶点着色器，因此它们的类型由顶点着色器的输出类型来确定，面片具有几个控制点，模板的第二个参数就设定为几
2. `SV_PrimitiveID`语义提供了面片的ID值，此ID唯一地标识了绘制调用过程中的各个面片，我们可以根据具体的需求来运用它。

InputPatch输入的数据由顶点摆放顺序决定（示例中为左上右下）

```c++
XMFLOAT3 quadVertices[4] = {
    XMFLOAT3(-0.54f, 0.72f, 0.0f),    // 左上角
    XMFLOAT3(0.54f, 0.72f, 0.0f),    // 右上角
    XMFLOAT3(-0.54f, -0.72f, 0.0f),    // 左下角
    XMFLOAT3(0.54f, -0.72f, 0.0f)    // 右下角
};
```

- 对**四边形面片（quad）**进行镶嵌化处理的过程由两个构成：

4个边缘曲面细分因子控制着对应边缘镶嵌后的份数

两个内部曲面细分因子指示了如何来对该四边形面片的内部进行镶嵌化处理（其中一个针对四边形的横向维度，另一个则作用于四边形的纵向维度）

- 对**三角形面片（tri）**进行镶嵌化处理的过程同样分为两部分：

1. 3个边缘曲面细分因子控制着对应边缘镶嵌后的份数
2. 一个内部曲面细分因子指示着三角形面片内部的镶嵌份数。

- 对**等值线（isoline）**进行镶嵌化处理的过程如下：

1. 2个边缘细分因子控制着等值线如何进行镶嵌。第一个值暂时不知道作用（忽略），第二个用于控制两个相邻控制点之间分成多少段。

#### D3D11硬件所支持的最大曲面细分因子为64（`D3D11_TESSELLATOR_MAX_TESSELLATION_FACTOR`）

如果将所有的曲面细分因子都设置为0，则该面片会被后续的处理阶段所丢弃

1. 如果面片根本没有出现在视锥体范围内，那么就能将它从后续的处理中丢弃（倘若已经对该面片进行了镶嵌化处理，那么其细分后的各三角形将在三角形裁剪期间被抛弃）
2. 如果面片是背面朝向的，那么就能将其从后面的处理过程中丢弃（如果该面片已经过了镶嵌化处理，则其细分后的所有三角形会在光栅化阶段的背面剔除过程中被抛弃）

#### 镶嵌化处理的次数取决于：

- **物体与摄像机之间的距离**
- **物体占用屏幕的范围**
- **三角形的朝向**:位于物体轮廓线上的三角形拥有更多细节
- **粗糙程度**：越粗糙越需要更细致的曲面细分

### 控制点外壳着色器

控制点外壳着色器以大量控制点作为输入和输出，顶点着色器每输出一个控制点，都会调用一次控制点外壳着色器，其应用之一是改变曲面的表示方式

```HLSL
[domain("quad")]
[partitioning("integer")]
[outputtopology("triangle_cw")]
[outputcontrolpoints(4)]
[patchconstantfunc("QuadConstantHS")]
[maxtessfactor(64.0f)]
```

**<font size=4>控制点外壳着色器包含以下几种属性,上方是定义它们的方式</font>**

1. `domain`：面片的类型。可选用的参数有`tri`（三角形面片）、`quad`（四边形面片）或`isoline`（等值线）
2. `partioning`：指定了曲面细分的细分模式。
   - `integer`：新顶点的添加或移除依据的是**上取整的函数**。例如我们将细分值设为3.25f时，实际上它将会细分为4份。这样一来，在网格随着曲面细分级别而改变时，会容易发生明显的跃变。
   - 非整型曲面细分（`fractional_even`/`fractional_odd`）：新顶点的增加或移除取决于曲面细分因子的整数部分，但是细微的渐变“过渡”调整就要根据细分因子的小数部分。当我们希望将粗糙的网格经曲面细分而平滑地过渡到具有更加细节的网格时，该参数就派上用场了。
   - `pow2`：目前测试的时候行为和`integer`一致，不知道什么原因。这里暂时不讲述。
3. `outputtopology`：通过细分所创的三角形的绕序
   - `triangle_cw`：顺时针方向的绕序
   - `triangle_ccw`：逆时针方向的绕序
   - `line`：针对线段的曲面细分
4. `outputcontrolpoints`：外壳着色器执行的次数，每次执行都输出1个控制点。系统值`SV_OutputControlPointID`给出的索引标明了当前正在工作的外壳着色器所输出的控制点。
5. `patchconstantfunc`：指定常量外壳着色器函数名称的字符串
6. `maxtessfactor`：告知驱动程序，用户在着色器中所用的曲面细分因子的最大值。如果硬件知道了此上限，就可以了解曲面细分所需的资源，继而在后台对此进行优化。Direct3D 11硬件支持的曲面细分因子最大值为64

### 镶嵌器阶段

硬件会基于常量外壳着色器程序所输出的曲面细分因子，对面片进行镶嵌化处理

## 域着色器

镶嵌器阶段会输出新建的顶点与三角形，所有在镶嵌器阶段创建的顶点，都会逐一调用域着色器

域着色器的输入为**常量外壳着色器输出的结构体**（包含曲面细化因子和一些附加信息）、控制点外壳着色器所输出的所有面片控制点、镶嵌化处理后的**顶点参数位置**（以重心坐标系(alpha,beta,gamma)的形式表示）.

域着色器输出的并不是镶嵌化处理之后的实际顶点坐标，而是这些点位于面片域空间内的参数坐标

#### 三角形的域着色器代码

```HLSL
struct VertexOut
{
    float3 PosL : POSITION;
};

typedef VertexOut HullOut;

// Tessellation_Triangle_DS.hlsl

[domain("tri")]
float4 DS(TriPatchTess patchTess,
    float3 weights : SV_DomainLocation,
    const OutputPatch<HullOut, 3> tri) : SV_POSITION
{
    // 重心坐标系插值
    float3 pos = tri[0].PosL * weights[0] +
        tri[1].PosL * weights[1] +
        tri[2].PosL * weights[2];
    
    return float4(pos, 1.0f);
}
```

矩形的域着色器代码

```HLSL
struct VertexOut
{
    float3 PosL : POSITION;
};

typedef VertexOut HullOut;

// Tessellation_Quad_DS.hlsl

[domain("quad")]
float4 DS(QuadPatchTess patchTess,
    float2 uv : SV_DomainLocation,
    const OutputPatch<HullOut, 4> quad) : SV_POSITION
{
    // 双线性插值
    float3 v1 = lerp(quad[0].PosL, quad[1].PosL, uv.x);
    float3 v2 = lerp(quad[2].PosL, quad[3].PosL, uv.x);
    float3 p = lerp(v1, v2, uv.y);
    
    return float4(p, 1.0f);
}
```

## 贝塞尔曲线

可以利用贝塞尔曲线，在控制点之间构造一条光滑的曲线

对于复杂曲线，可以将曲线分段，再用低阶贝塞尔曲线去拟合，减少计算量

## 贝塞尔曲面

三阶贝塞尔曲线有4个控制点，我们可以将三阶贝塞尔曲面看作是4条三阶贝塞尔曲线，先求得四条贝塞尔曲线上的四个点，再以这四个点为新的控制点，就能求出曲面上的一点
